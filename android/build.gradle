buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'com.adarshr:gradle-test-logger-plugin:2.0.0'
  }
}

apply plugin: 'com.android.library'
apply plugin: "com.adarshr.test-logger"

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 29)
  buildToolsVersion safeExtGet('buildToolsVersion', '29.0.3')

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 16)
    compileSdkVersion safeExtGet('compileSdkVersion', 29)
    targetSdkVersion safeExtGet('targetSdkVersion', 29)
    versionCode 1
    versionName "1.0"
  }

  lintOptions {
    abortOnError false
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'com.facebook.react:react-native:+'

  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'androidx.legacy:legacy-support-v4:1.0.0'

  /* https://mvnrepository.com/artifact/androidx.biometric/biometric
      src: http://bit.ly/31DhLZG */
  implementation 'androidx.biometric:biometric:1.0.1@aar'

  /* version higher 1.1.3 has problems with included soloader packages,
      https://github.com/facebook/conceal/releases */
  implementation "com.facebook.conceal:conceal:1.1.3@aar"

  /* Unit Testing Frameworks */
  testImplementation 'junit:junit:4.13'

  /* Mockito, https://mvnrepository.com/artifact/org.mockito/mockito-inline */
  testImplementation 'org.mockito:mockito-inline:3.2.4'

  /* https://mvnrepository.com/artifact/org.hamcrest/hamcrest/2.1 */
  testImplementation 'org.hamcrest:hamcrest:2.2'

  /* http://robolectric.org/getting-started/ */
  testImplementation("org.robolectric:robolectric:4.3.1")

  /* https://mvnrepository.com/artifact/androidx.test.ext/junit */
  testImplementation "androidx.test.ext:junit:1.1.2-alpha03"

  /* https://mvnrepository.com/artifact/androidx.test/core */
  testImplementation 'androidx.test:core:1.3.0-alpha03'
  testImplementation "androidx.test:runner:1.3.0-alpha03"
  testImplementation "androidx.test:monitor:1.3.0-alpha03"

  // Uncomment for including JRE implementation of crypto api (that is used in Robolectric tests)
  // testImplementation fileTree(dir: "${System.properties.get("java.home")}/lib", include: ["jce.jar"])
}
